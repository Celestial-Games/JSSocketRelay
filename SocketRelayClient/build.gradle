plugins {
    id 'java-library'
    id "edu.sc.seis.launch4j" version "2.4.6"
}

group = 'SocketRelay'
version = '1.0.0'

apply plugin: 'maven-publish'
publishing {
	publications {
		maven(MavenPublication) {
			groupId = 'com.socketrelay'
			artifactId = 'socketrelay-client'
			version = '1.0.0'

			from components.java
		}
	}
}

configurations {
    compile {
        transitive = false
    }
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
	implementation group: 'com.socketrelay', name: 'socketrelay-common', version: '1.0.0'

	implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
	implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
	implementation group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.30'
	implementation group: 'org.apache.mina', name: 'mina-core', version: '2.1.3'
}

project.configurations.implementation.setCanBeResolved(true)

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version,
                   'Main-Class': 'com.socketrelay.client.SocketRelayClient',
                   'Class-Path': configurations.implementation.collect { it.name }.join(' ')
                   )
    }
}

task cleanDest(type: Delete) {
    delete 'build/dest'
}

task copyTask(type: Copy) {
    from file('config.json')
    into file('build/dest/SocketRelayClient')
}

task createJavaExe(type: edu.sc.seis.launch4j.tasks.Launch4jLibraryTask) {
    outfile = 'SocketRelayClient.exe'
	mainClassName = 'com.socketrelay.client.SocketRelayClient'
    icon = "${projectDir}/icon.ico"
    fileDescription = 'The Scoket Relay Client.'
    jreMinVersion=1.8
    outputDir="dest/SocketRelayClient"
}

task zipClient(type: Zip) {
	from 'build/dest'
	include '*'
	include '*/**'
	archiveName 'SocketRelayClient.zip'
	//destinationDir(file('/dir1/dir2/dir3/'))
}

task buildLib(dependsOn: [cleanDest, jar, createJavaExe, copyTask, publishToMavenLocal, zipClient])